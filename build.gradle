ext {
  springbootVersion = "2.7.17"
  springDependencyManagementVersion = "1.0.15.RELEASE"
  springIntegrationVersion = "5.5.19"

//  springbootVersion = "3.2.2"
//  springDependencyManagementVersion = "1.1.4"
//  springIntegrationVersion = "6.2.1"

  lombokVersion = "1.18.30"
  jacksonVersion = "2.16.1"
  guavaVersion = "33.0.0-jre"
  caffeineVersion = '3.1.8'
  fastjson2Version = "2.0.47"
  commonsIOVersion = "2.15.1"
  commonsLang3Version = "3.14.0"
  //slf4jVersion = "2.0.11"
  slf4jVersion = "1.7.36"
  hutoolVersion = "5.8.25"
  cglibVersion = "3.3.0"
  nettyAllVersion = "4.1.107.Final"

  aspectjVersion = "1.9.20"
  swaggerVersion = "3.0.0"
  knife4jVersion = "4.3.0"
  jjwtVersion = "0.12.3"
  vertxVersion = "4.4.6"

  rxJavaVersion = '3.1.8'
  okHttpVersion = '4.12.0'
  retrofitVersion = '2.9.0'

  mysqlVersion = "8.2.0"
  postgresqlVersion = "42.6.0"
  druidVersion = "1.2.20"
  mybatisPlusVersion = "3.5.4.1"
  pagehelperVersion = "2.0.0"

  try {
    def props = new Properties()
    props.load(new FileReader("${getRootDir()}/nexus.properties"))
    props.forEach(ext::set)
  } catch (ignored) {/*^_^*/}

}

subprojects {

  apply plugin: 'java'

  group = "com.benefitj.spring"
  version = "2.4.2-SNAPSHOT"

  ext {
    compatibilityVersion = JavaVersion.VERSION_17
    //scriptFile = "https://gitee.com/axiu8888/GradleBuild/raw/main/Java/base.gradle"
    scriptFile = "${rootDir}/gradleScript/base.gradle"

    groupId = "$group"
    artifactPrefix = ""
    artifactId = "${getProject().name}"
    versionId = "${version}"

    springEnv = true

    manifestAttributes = [
        "version"   : "$version",
        "build-date": "${new Date().format('yyyy-MM-dd')}",
        "author"    : "axiu8888",
        "home"      : "https://github.com/axiu8888/${rootProject.name}",
    ]
  }

  dependencies {
    def commons = project(":commons")
    //println("project ==>: ${project.name}")
    if (project != commons) {
      compileOnly(commons)
      testImplementation(commons)
    }
  }

  repositories {
    maven { url "https://maven.aliyun.com/repository/google" }
    maven { url "https://maven.aliyun.com/repository/central" }
    maven { url "https://maven.aliyun.com/repository/gradle-plugin" }
    maven { url "https://maven.aliyun.com/repository/public" }
    maven { url "https://maven.aliyun.com/repository/jcenter" }
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://repo.spring.io/release" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    mavenCentral()

    def nexusFile = file("${rootDir}/nexus.properties")
    if (nexusFile.exists() && nexusFile.length() > 0) {
      def props = new Properties()
      props.load(nexusFile.newReader("UTF-8"))
      maven {
        url = props["repoUrl"]
        allowInsecureProtocol = true
        credentials {
          username props["repoUsername"]
          password props["repoPassword"]
        }
      }
    }
  }
}
